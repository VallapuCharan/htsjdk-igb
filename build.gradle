buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'osgi'

}

repositories {
    mavenCentral()
}

dependencies {
    compile "commons-logging:commons-logging:1.2"
    compile "org.apache.commons:commons-jexl:2.1.1"
    compile "org.apache.commons:commons-compress:1.4.1"
    compile "org.tukaani:xz:1.5"
 }

sourceCompatibility = 1.8
targetCompatibility = 1.8

final isRelease = Boolean.getBoolean("release")
version = '2.16.2'

logger.info("build for version:" + version)

defaultTasks 'jar'

jar {
  archiveName = 'htsjdk-igb-'+version+".jar"

  manifest { // the manifest of the default jar is of type OsgiManifest
      name = 'htsjdk for IGB'
      instruction 'Bundle-Vendor', 'Loraine Lab'
      instruction 'Bundle-Description', 'htsjdk for IGB'
      instruction 'Bundle-DocURL', 'https://github.com/lorainelab/htsjdk'

      }
}

wrapper {
    gradleVersion = '4.8.1'
}

// This is a hack to disable the java 8 default javadoc lint until we fix the html formatting
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

/**
 *This specifies what artifacts will be built and uploaded when performing a maven upload.
 */
artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

/**
 * Upload a release to sonatype.  You must be an authorized uploader and have your sonatype
 * username and password information in your gradle properties file.  See the readme for more info.
 *
 * For releasing to your local maven repo, use gradle install
 */
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: project.findProperty("igb-htsjdk-release-repo")) {
                authentication(userName: project.findProperty("igb-htsjdk-release-username"), password: project.findProperty("igb-htsjdk-release-repo-psswd"))
            }
            pom.project{
                version = version
                artifactId = "htsjdk-igb"
                groupId = "htsjdk-igb"
                packaging = "bundle"
            }
        }
    }
    doFirst{
        System.out.println("Uploading artifact")
    }
}